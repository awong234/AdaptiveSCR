
model {
 # year-specific N parameterized in DA parameter 

p0 ~ dunif(0,1)
sigma ~ dunif(0,10)
psi<- sum(lambda[])/bigM
beta.cov ~ dnorm(0,.01)

A <- ((xlim[2]-xlim[1]))*((ylim[2]-ylim[1]))

for(t in 1:Robs){
 for(i in 1:bigM){
   ingroup[i,t]<- grid.id[i] == t
   ingroup2[i,t]<- z[i]*ingroup[i,t]
}
 N[t] <- sum(ingroup2[,t])   
 ######D[t] <- (N[t]/A)*10000  # put in units of per ha
 pi[t]<- lambda[t]/sum(lambda[])
}
 Ntotal.scr<- sum(N)
 Ntotal.index<- sum(Nindex)
 Ntotal<- Ntotal.scr + Ntotal.index

beta0 ~ dnorm(0,0.01)

for(t in 1:Rindex){
 lam.index[t]<- exp(beta0)   # Same PRIOR mean abundance as other sites
 Nindex[t] ~ dpois(lam.index[t])
 lam.cov2[t]<- exp(beta.cov)*Nindex[t]  
 cov.index[t] ~ dpois(lam.cov2[t])
}

for(t in 1:Robs){
 log(lambda[t])<- beta0
 lam.cov[t]<- exp(beta.cov)*N[t]  
 cov.scr[t] ~ dpois(lam.cov[t])
} 

# Model for the SCR sites
for(i in 1:bigM){
  z[i] ~ dbern(psi)
  grid.id[i] ~ dcat(pi[])
  S[i,1] ~ dunif(xlim[1],xlim[2])
  S[i,2] ~ dunif(ylim[1],ylim[2])

  for(j in 1:ntraps){
      d2[i,j] <- pow(pow(S[i,1]-X[j,1],2) + pow(S[i,2]-X[j,2],2),1)
      cp[i,j] <- z[i]*p0*exp(-d2[i,j]/(2*sigma*sigma))
      Y[i,j] ~ dbin(cp[i,j],K)
     }
 
}
}
